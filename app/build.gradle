apply plugin: 'com.android.application'

// 赋值与引用
def androidId = rootProject.ext.androidId
def appId = rootProject.ext.appId
def support = rootProject.ext.dependencies

android {
    compileSdkVersion androidId.compileSdkVersion
    buildToolsVersion androidId.buildToolsVersion
    defaultConfig {
        applicationId appId.applicationId
        minSdkVersion androidId.minSdkVersion
        targetSdkVersion androidId.targetSdkVersion
        versionCode androidId.versionCode
        versionName androidId.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // 在gradle文件中配置选项参数值（用于APT传参接收）
        // 切记：必须写在defaultConfig节点下
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName(), packageNameForAPT: packageNameForAPT]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // 标准写法
    // implementation group: 'com.android.support', name:'appcompat-v7', version:'28.0.0'
    // 简写
    // implementation 'com.android.support:appcompat-v7:28.0.0'

    // 依赖第三方库最简洁的方式：
    support.each { k, v -> implementation v } // 上面已经定义过support，是在Project的config.gradle中统一管理的

    implementation project(':common') // 公共基础库
    implementation project(':router_annotation') // 注解
    annotationProcessor project(':router_compiler') // 注解处理器

    if (isRelease) {
        implementation project(':order')
        implementation project(':shoppingcar')
    }
}
